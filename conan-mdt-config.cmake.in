@PACKAGE_INIT@

if(CMAKE_VERSION VERSION_LESS 3.1.0)
    message(FATAL_ERROR "@MDT_CONFIG_INSTALL_NAMESPACE@ requires at least CMake version 3.1.0")
endif()

# See https://gitlab.com/scandyna/mdtcmakeconfig/-/issues/2
if(NOT @MDT_CONFIG_INSTALL_NAMESPACE@_FIND_COMPONENTS)
  set(@MDT_CONFIG_INSTALL_NAMESPACE@_NOT_FOUND_MESSAGE "The @MDT_CONFIG_INSTALL_NAMESPACE@ package requires at least one component. Maybe this file was included accidentally by a find_dependency() call because MdtCMakeConfig was declared in the requires section of a conanfile (must be declared as a build_requires)")
  set(@MDT_CONFIG_INSTALL_NAMESPACE@_FOUND False)
  return()
endif()

foreach(component ${@MDT_CONFIG_INSTALL_NAMESPACE@_FIND_COMPONENTS})
  # Limit the search to CMAKE_PREFIX_PATH
  find_package(
    @MDT_CONFIG_INSTALL_NAMESPACE@${component}
    ${@MDT_CONFIG_INSTALL_NAMESPACE@_FIND_VERSION}
    QUIET CONFIG
    NO_CMAKE_ENVIRONMENT_PATH NO_SYSTEM_ENVIRONMENT_PATH NO_CMAKE_PACKAGE_REGISTRY NO_CMAKE_SYSTEM_PATH NO_CMAKE_SYSTEM_PACKAGE_REGISTRY
  )
  if(NOT @MDT_CONFIG_INSTALL_NAMESPACE@${component}_FOUND AND ${@MDT_CONFIG_INSTALL_NAMESPACE@_FIND_REQUIRED_${component}})
    set(@MDT_CONFIG_INSTALL_NAMESPACE@_NOT_FOUND_MESSAGE "Failed to find @MDT_CONFIG_INSTALL_NAMESPACE@ component ${component} config file (@MDT_CONFIG_INSTALL_NAMESPACE@${component}Config.cmake)")
    set(@MDT_CONFIG_INSTALL_NAMESPACE@_FOUND False)
    break()
  endif()
endforeach()
